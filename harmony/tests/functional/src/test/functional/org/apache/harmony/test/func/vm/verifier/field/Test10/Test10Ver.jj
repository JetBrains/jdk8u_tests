;  Licensed to the Apache Software Foundation (ASF) under one or more
;  contributor license agreements.  See the NOTICE file distributed with
;  this work for additional information regarding copyright ownership.
;  The ASF licenses this file to You under the Apache License, Version 2.0
;  (the "License"); you may not use this file except in compliance with
;  the License.  You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
;  Unless required by applicable law or agreed to in writing, software
;  distributed under the License is distributed on an "AS IS" BASIS,
;  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
;  See the License for the specific language governing permissions and
;  limitations under the License.
;
;
;

.class public org/apache/harmony/test/func/vm/verifier/field/Test10/Test10Ver
.super java/lang/Object
.field public testFieldB Lorg/apache/harmony/test/func/vm/verifier/field/Test10/Test10B;

;
; standard initializer
.method public <init>()V
   aload_0
   invokespecial java/lang/Object/<init>()V
   return
.end method

;
; test method
.method public test()V
   .limit stack 5
   .limit locals 3

; store 0 to localvariable2
   sipush 0
   istore_2

; push Object, E, Object
   new java/lang/Object
   dup
   invokespecial java/lang/Object/<init>()V
   aload_0
   new org/apache/harmony/test/func/vm/verifier/field/Test10/Test10E
   dup
   invokespecial org/apache/harmony/test/func/vm/verifier/field/Test10/Test10E/<init>()V
   new java/lang/Object
   dup
   invokespecial java/lang/Object/<init>()V

lab:
   iinc 2 1 ; localvariable2++
   pop
   putfield org/apache/harmony/test/func/vm/verifier/field/Test10/Test10Ver/testFieldB Lorg/apache/harmony/test/func/vm/verifier/field/Test10/Test10B;
   pop

   iload_2
   tableswitch 1
        l1 ; if 1 jump to l1
        l2 ; if 2 jump to l2
        l3 ; if 3 jump to l3
     default: def ; jump to def

l1:
; push Thread, E, Thread
   new java/lang/Thread
   dup
   invokespecial java/lang/Thread/<init>()V
   aload_0
   new org/apache/harmony/test/func/vm/verifier/field/Test10/Test10E
   dup
   invokespecial org/apache/harmony/test/func/vm/verifier/field/Test10/Test10E/<init>()V
   new java/lang/Thread
   dup
   invokespecial java/lang/Thread/<init>()V
   goto lab

l2:
; push String, E, String
   new java/lang/String
   dup
   invokespecial java/lang/String/<init>()V
   aload_0
   new org/apache/harmony/test/func/vm/verifier/field/Test10/Test10E
   dup
   invokespecial org/apache/harmony/test/func/vm/verifier/field/Test10/Test10E/<init>()V
   new java/lang/String
   dup
   invokespecial java/lang/String/<init>()V
   goto lab

l3:
; push E, E or Object, E
   new org/apache/harmony/test/func/vm/verifier/field/Test10/Test10E
   dup
   invokespecial org/apache/harmony/test/func/vm/verifier/field/Test10/Test10E/<init>()V

   aload_0
   iconst_1
   ifeq l4
   new org/apache/harmony/test/func/vm/verifier/field/Test10/Test10E
   dup
   invokespecial org/apache/harmony/test/func/vm/verifier/field/Test10/Test10E/<init>()V
   goto l5
l4:
   new java/lang/Object
   dup
   invokespecial java/lang/Object/<init>()V
l5:

   new org/apache/harmony/test/func/vm/verifier/field/Test10/Test10E
   dup
   invokespecial org/apache/harmony/test/func/vm/verifier/field/Test10/Test10E/<init>()V
   goto lab

def:
   return
.end method
